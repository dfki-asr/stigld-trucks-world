prefix ex: <http://example.org/>
prefix xsd: <http://www.w3.org/2001/XMLSchema#>
prefix rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
prefix st: <http://dfki.de/stigld#>
prefix stigFN: <http://www.dfki.de/func#>
DELETE {
    ?area ex:status ex:empty.
    ?package ex:located ?location.
}
INSERT {
    ?task a ex:Task , ex:PickupTask; ex:startTime ?now; ex:endTime ?endTime ; ex:assignedTo ?truck ; ex:driveTime ?driveTime.
    ?area ex:status ex:occupied.
    ?package ex:located ?area.

} WHERE{
    { SELECT (UUID() as ?task) ?truck ?location WHERE {
        ?truck a ex:Truck ; ex:located ?location .
        FILTER NOT EXISTS {
          ?anyTask ex:assignedTo ?truck ;
        }
    }}

    { SELECT (COUNT(?empty) as ?numEmpty) ?truck WHERE {
        ?truck a ex:Truck ; ex:areas ?empty .
        ?empty a ex:Area ; ex:status ex:empty .
    } GROUP BY ?truck}

    { SELECT (COUNT(?package) as ?numUrgent) ?location ?truck WHERE {
        ?package a ex:Package ; ex:located ?location ; st:carries [ a ex:UrgencyStigma ; ex:forTruck ?truck ] .
    } GROUP BY ?location ?truck}

    FILTER( ?numEmpty <= ?numUrgent)

    { SELECT ?truck ?package ?location WHERE {
      ?truck a ex:Truck ; ex:located ?location .
      ?package a ex:Package; ex:located ?location ; st:carries [ a ex:UrgencyStigma ; ex:forTruck ?truck ] .
      ?location st:adjacentTo ?dest; ex:driveTime [ ex:destination ?dest; rdf:value  ?driveTime] .
      ?g a ex:Goal; ex:payload ?package; ex:destination ?dest ; ex:deadline ?deadline.
    } ORDER BY DESC(?driveTime) LIMIT 1}

    ### Choose innermost area in the truck loading bay
    {SELECT * WHERE {
        ?truck a ex:Truck; ex:located ?location; ex:areas ?area.
        ?area a ex:Area; ex:status ex:empty; ex:position ?pos.
    }ORDER BY DESC(?pos) LIMIT 1}

    OPTIONAL { ex:Clock rdf:value ?clocktime	}
    BIND(IF(bound(?clocktime), ?clocktime, 0) as ?now)
    BIND((?now + 0.2) as ?endTime )
}
